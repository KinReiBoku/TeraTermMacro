; バージョン 4.61以降
; ホスト設定ファイルディレクトリ
dir_hostconf = ''
; ログディレクトリ
dir_log = ''


; 処理開始


; 現在のディレクトリを取得する
getdir dir
strmatch dir '.*\$'
if result = 0 then
	strconcat dir '\'
endif

; ホスト設定ファイルのディレクトリパスを生成する
strlen dir_hostconf
if result = 0 then
	sprintf2 dir_hostconf '%s%s' dir 'include\'
endif

; ログディレクトリパスを生成する
strlen dir_log
if result = 0 then
	sprintf2 dir_log '%s%s' dir 'log\'
endif

; ホスト設定ファイルを検索する
setdir dir_hostconf
extpat='*.conf'
array_host_cnt = 1

findfirst dh extpat filename

while result
	array_host_cnt = array_host_cnt+1
	findnext dh filename
endwhile

findclose dh

; リストボックス
strdim array_host array_host_cnt
array_host[0] = '手動入力する'
array_host_cnt = array_host_cnt-1

findfirst dh extpat filename

i = 1
while array_host_cnt
	strreplace filename 1 '\.conf' ''
	array_host[i] = filename
	i = i+1
	array_host_cnt = array_host_cnt-1
	findnext dh filename
endwhile

findclose dh

; リストボックスを表示する
listbox '接続先を選んでください' '接続先選択' array_host

; 入力を待避する
ret = result

; キャンセルされた場合
if ret == -1 then
	end
; 手動入力の場合
elseif ret == 0 then
	; ホスト名入力
	inputbox 'ホスト名またはIPアドレスを入力してください' 'ホスト名'
	host = inputstr
	strlen host 
	if result = 0 then 
		messagebox '入力エラー：終了します' '入力エラー' 
		end 
	endif
	; ユーザ名入力
	inputbox 'ユーザ名を入力してください' 'ユーザ名'
	user = inputstr
	strlen user 
	if result = 0 then 
		messagebox '入力エラー：終了します' '入力エラー' 
		end 
	endif
	; パスワード名入力
	inputbox 'パスワードを入力してください' 'パスワード'
	pass = inputstr
	strlen pass 
	if result = 0 then 
		messagebox '入力エラー：終了します' '入力エラー' 
		end 
	endif
; それ以外の場合
else
	; 初期化する
	include_hostconf = ''
	; 設定ファイル名（ホスト名.conf）を生成する
	sprintf2 file_hostconf '%s%s.conf' dir_hostconf array_host[ret]
	; 設定ファイルを読込む
	include file_hostconf

	; 接続情報配列カウンタを初期化する
	array_connect_cnt = 1

	; インクルード先の設定ファイルがある場合
	strlen include_hostconf
	while result > 0
		; 接続情報配列カウンタをインクリメントする
		array_connect_cnt = array_connect_cnt+1
		; インクルード先の設定ファイル名（ホスト名.conf）を生成する
		sprintf2 file_hostconf_include '%s%s' dir_hostconf include_hostconf
		; 変数をクリア
		include_hostconf=''
		; インクルード先の設定ファイルを読込む
		include file_hostconf_include
		strlen include_hostconf
	endwhile

	; 接続情報配列カウンタを生成する
	strdim array_connect_host array_connect_cnt
	strdim array_connect_user array_connect_cnt
	strdim array_connect_pass array_connect_cnt

	; 設定ファイル名（ホスト名.conf）を生成する
	sprintf2 file_hostconf '%s%s.conf' dir_hostconf array_host[ret]
	; 設定ファイルを読込む
	include file_hostconf

	; カウンタを初期化する
	cnt = array_connect_cnt-1

	; 接続情報を代入する
	array_connect_host[cnt] = host
	array_connect_user[cnt] = user
	array_connect_pass[cnt] = pass

	while cnt > 0
		; インクルード先の設定ファイル名（ホスト名.conf）を生成する
		sprintf2 file_hostconf_include '%s%s' dir_hostconf include_hostconf
		; 設定ファイルを読込む
		include file_hostconf_include
		; カウンタをデクリメントする
		cnt = cnt-1
		; 接続情報を代入する
		array_connect_host[cnt] = host
		array_connect_user[cnt] = user
		array_connect_pass[cnt] = pass
	endwhile
endif

; カウンタを初期化する
cnt = 0

; connectで使う文字列を生成する
sprintf2 data_connect '/ssh %s /auth=password /user=%s /passwd=%s' array_connect_host[cnt] array_connect_user[cnt] array_connect_pass[cnt]

; 接続する
connect data_connect

; カウンタをインクリメントする
cnt = cnt+1

; タイムスタンプを取得する
gettime data_time "%Y%m%d_%H%M%S"
; ログファイル名を生成する
sprintf2 file_log '%s%s_teraterm_%s.log' dir_log array_host[ret] data_time
; ログを開始する
logopen file_log 0 1 0 1
logwrite array_host[ret]
logwrite #13#10#13#10
logwrite 'Connect: '
logwrite data_connect
logwrite #13#10#13#10

; プロンプトを代入する
prompt1 = '#'
prompt2 = '$'

; プロンプトを待つ
wait prompt1 prompt2

while array_connect_cnt > cnt
	; 接続情報を代入する
	host = array_connect_host[cnt]
	user = array_connect_user[cnt]
	pass = array_connect_pass[cnt]
	; ssh接続する
	command = ''
	strconcat command 'ssh -XC '
	strconcat command user
	strconcat command '@'
	strconcat command host
	sendln command
	wait "password: "
	sendln pass
	wait prompt1 prompt2
	; カウンタをインクリメントする
	cnt = cnt+1
endwhile

; マクロ終了
end
